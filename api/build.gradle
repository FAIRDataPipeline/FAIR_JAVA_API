plugins {
    id 'java'
    id "com.diffplug.gradle.spotless" version "4.4.0"
    id "maven-publish"
    id "signing"
    id "jacoco"
}

group 'org.fairdatapipeline'
version '1.0.0-rc1'

repositories {
    maven {
        url "https://artifacts.unidata.ucar.edu/repository/unidata-all/"
    }
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.overview = "src/main/java/org/fairdatapipeline/overview.html"
}


dependencies {
    implementation project(':dataregistry')
    implementation 'org.jetbrains:annotations:20.1.0'
    annotationProcessor 'org.immutables:value:2.9.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.maven:maven-artifact:3.8.4'
    implementation 'org.glassfish.jersey.core:jersey-client:3.0.4'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.0.4'
    implementation 'org.glassfish.jersey:jersey-bom:3.0.4'
    implementation 'org.immutables:value-annotations:2.9.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.2'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.13.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.13.2'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'edu.ucar:cdm-core:5.5.3'
    implementation 'edu.ucar:netcdf4:5.5.3'
    testAnnotationProcessor 'org.immutables:value:2.9.0'
    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.javatuples:javatuples:1.2'
    testImplementation 'org.apache.commons:commons-compress:1.21'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'api'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'javaDataPipeline API'
                description = 'javaDataPipeline API'
                url = 'http://www.fairdatapipeline.org/'
                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE, Version 3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                    }
                }
                organization {
                    name = 'THE SCOTTISH COVID-19 RESPONSE CONSORTIUM'
                    url = 'https://www.gla.ac.uk/research/az/scrc/'
                }
                developers {
                    developer {
                        id = 'bram'
                        name = 'Bram Boskamp'
                        email = 'bram@boska.mp'
                        organization = 'Biomathematics and Statistics Scotland'
                        organizationUrl = 'https://www.bioss.ac.uk/'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/FAIRDataPipeline/javaDataPipeline.git'
                    developerConnection = 'scm:git:https://github.com/FAIRDataPipeline/javaDataPipeline.git'
                    url = 'https://github.com/FAIRDataPipeline/javaDataPipeline'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username findProperty('ossrhUsername')
                password findProperty('ossrhPassword')
            }
        }
    }
}

signing {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.secretKeyRingFile')) {
        sign publishing.publications.mavenJava
    }
}

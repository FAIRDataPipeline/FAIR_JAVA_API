plugins {
    id 'java'
    id "com.diffplug.gradle.spotless" version "4.4.0"
    id "maven-publish"
    id "jacoco"
}

group 'org.fairdatapipeline'
version '0.0.2'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.7.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'org.hamcrest:hamcrest:2.2'
    implementation 'org.glassfish.jersey.core:jersey-client:3.0.1'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.0.1'
    implementation 'org.glassfish.jersey:jersey-bom:3.0.1'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0'
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'commons-io:commons-io:2.7'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'jakarta.activation:jakarta.activation-api:2.0.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation('org.slf4j:slf4j-simple:1.7.25')
    testImplementation 'org.mockito:mockito-core:2.28.2'
}

spotless {
    java {
        googleJavaFormat()
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/FAIRDataPipeline/javaDataPipeline")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("PUBLISH_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PUBLISH_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}
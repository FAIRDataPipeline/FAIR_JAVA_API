plugins {
    id 'java'
    id "com.diffplug.gradle.spotless" version "4.4.0"
    id "maven-publish"
    id "signing"
    id "jacoco"
}

group 'org.fairdatapipeline'
version '1.0.0-beta'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

javadoc {
        options.overview = "src/main/java/org/fairdatapipeline/dataregistry/overview.html"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    implementation 'org.glassfish.jersey.core:jersey-client:3.0.4'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.0.4'
    implementation 'org.glassfish.jersey:jersey-bom:3.0.4'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0'
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.13.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'jakarta.activation:jakarta.activation-api:2.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
}

spotless {
    java {
        googleJavaFormat()
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'dataregistry'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'javaDataPipeline Dataregistry'
                description = 'The library used by javaDataPipeline API to access the FAIR DataPipeline Registry'
                url = 'http://www.fairdatapipeline.org/'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                organization {
                        name = 'THE SCOTTISH COVID-19 RESPONSE CONSORTIUM'
                        url = 'https://www.gla.ac.uk/research/az/scrc/'
                }
                developers {
                    developer {
                        id = 'bram'
                        name = 'Bram Boskamp'
                        email = 'bram@boska.mp'
                        organization = 'Biomathematics and Statistics Scotland'
                        organizationUrl = 'https://www.bioss.ac.uk/'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/FAIRDataPipeline/javaDataPipeline.git'
                    developerConnection = 'scm:git:https://github.com/FAIRDataPipeline/javaDataPipeline.git'
                    url = 'https://github.com/FAIRDataPipeline/javaDataPipeline'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username findProperty('ossrhUsername')
                password findProperty('ossrhPassword')
            }
        }
    }
}

signing {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.secretKeyRingFile')) {
        sign publishing.publications.mavenJava
    }
}

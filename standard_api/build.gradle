plugins {
    id 'java'
    id "com.diffplug.gradle.spotless" version "4.4.0"
    id "maven-publish"
    id "jacoco"
}

group 'uk.ramp'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs += ["-Xlint:unchecked"]
}

compileTestJava {
    options.compilerArgs += ["-Xlint:unchecked"]
}

dependencies {
    implementation project(':file_api')
    implementation project(':dataRegistry')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.12.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.12.2'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.immutables:value-annotations:2.8.8'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.assertj:assertj-guava:3.2.0'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    annotationProcessor "org.immutables:value:2.8.8"
}

spotless {
    java {
        googleJavaFormat()
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/scottishcovidresponse/data_pipeline_api")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("JAVA_API_PUBLISH_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("JAVA_API_PUBLISH_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    finalizedBy jacocoTestReport
}
jacoco {
    toolVersion = '0.8.7'
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}
